apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def versionMajor = 3
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "ca.josephroque.bowlingcompanion"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"

            def apiKeys = [
                    ["TRANSFER_SERVER_URL", "http://10.0.2.2:8080"],
                    ["BANNER_AD_UNIT_ID", "ca-app-pub-3940256099942544/6300978111"],
                    ["TRANSFER_API_KEY", "API_KEY_GOES_HERE"],
                    ["MIXPANEL_TOKEN", "API_KEY_GOES_HERE"],
                    ["BUGSNAG_TOKEN", "API_KEY_GOES_HERE"]
            ]

            apiKeys.each { key, value ->
                buildConfigField "String", key, "\"${value}\""
                resValue "string", key, value
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def apiKeys = [
                    ["TRANSFER_SERVER_URL", "API_KEY_GOES_HERE"],
                    ["BANNER_AD_UNIT_ID", "API_KEY_GOES_HERE"],
                    ["TRANSFER_API_KEY", "API_KEY_GOES_HERE"],
                    ["MIXPANEL_TOKEN", "API_KEY_GOES_HERE"],
                    ["BUGSNAG_TOKEN", "API_KEY_GOES_HERE"]
            ]

            apiKeys.each { key, value ->
                buildConfigField "String", key, "\"${value}\""
                resValue "string", key, value
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation "com.android.support:customtabs:28.0.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.2'
    implementation "android.arch.lifecycle:extensions:1.1.1"

    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation 'com.robertlevonyan.view:MaterialChipView:1.2.1'
    implementation 'com.ncapdevi:frag-nav:2.4.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    implementation "com.mixpanel.android:mixpanel-android:5.4.1"
    implementation 'com.bugsnag:bugsnag-android:4.7.0'

    implementation "com.google.android.gms:play-services-gcm:15.0.1"
    implementation 'com.google.android.gms:play-services-ads:15.0.1'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.24.0"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
